/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:		Crestron Electronics, Inc.
System Name:
System Number:
Programmer:			PAC modified by kdo
Comments:
*/
/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
// #ENABLE_DYNAMIC
#SYMBOL_NAME "Cisco Touch 10 Room Control DPad Buttons v1.0"
// #HINT ""
// #DEFINE_CONSTANT
// #CATEGORY "" 
#CATEGORY "20" // Conferencing
// #PRINT_TO_TRACE
// #DIGITAL_EXPAND
// #ANALOG_SERIAL_EXPAND
// #OUTPUT_SHIFT 
// #HELP_PDF_FILE ""
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
// #ENABLE_TRACE
/*#HELP_BEGIN
	to clear feedback for all buttons set the buttonActive to a null string ("").
	when the button on the Cisco panel is released the activeButton output will be set to
	null string ("").
#HELP_END*/
/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
BUFFER_INPUT	_skip_, fromProcessorModule[250];
DIGITAL_INPUT	_skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, unsetButtons, buttonActive[5, 5];

DIGITAL_OUTPUT	_skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, initialized, error;
STRING_OUTPUT	errorMessage, toProcessorModule;
DIGITAL_OUTPUT	activeButton[5, 5];
/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/
STRING_PARAMETER	paramWidgetId[250], paramGroupIds[5, 5][50];
/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
#BEGIN_PARAMETER_PROPERTIES paramWidgetId
	propValidUnits = unitString;
	propDefaultUnit = unitString;
	propDefaultValue = "widget_1";
	propShortDescription = "enter the widget id (this is case sensitive)";
#END_PARAMETER_PROPERTIES
#BEGIN_PARAMETER_PROPERTIES paramGroupIds
	propValidUnits = unitString;
	propDefaultUnit = unitString;
	propDefaultValue = "groupId";
	propShortDescription = "enter the group id (this is case sensitive)";
#END_PARAMETER_PROPERTIES
/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
INTEGER	initComplete;
/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
function processFromProcessor(string paramData)
{
	string tempData[250], tempCommand[250], actionId[250];
	integer tempStart, tempQuote, tempEnd, tempCount, a;
	
	tempData = paramData;
	tempCommand = remove(" ", tempData);
	if(tempCommand = "Send ")
	{
		makestring(toProcessorModule, "Send Name %d = \x22%s\x22\x0D\x0A", atoi(tempData), paramWidgetId);
		error = 0;
		errorMessage = "";
		initComplete = 1;
		initialized = initComplete;
		if(buttonActive[1] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[1], paramWidgetId);
		}
		else if(buttonActive[2] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[2], paramWidgetId);
		}
		else if(buttonActive[3] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[3], paramWidgetId);
		}
		else if(buttonActive[4] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[4], paramWidgetId);
		}
		else if(buttonActive[5] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[5], paramWidgetId);
		}

		else
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget UnsetValue WidgetId: \x22%s\x22\n", paramWidgetId);
		}
	}
	else if(tempCommand = "Update ")
	{
		if(buttonActive[1] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[1], paramWidgetId);
		}
		else if(buttonActive[2] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[2], paramWidgetId);
		}
		else if(buttonActive[3] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[3], paramWidgetId);
		}
		else if(buttonActive[4] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[4], paramWidgetId);
		}
		else if(buttonActive[5] = 1)
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[5], paramWidgetId);
		}

		else
		{
			makestring(toProcessorModule, "xCommand UserInterface Extensions Widget UnsetValue WidgetId: \x22%s\x22\n", paramWidgetId);
		}
	}
	else if(tempCommand = "#count ")
	{
		toProcessorModule = paramData;
	}
	else if(tempCommand = "#error: ")
	{
		initComplete = 0;
		initialized = initComplete;
		error = 1;
		errorMessage = left(tempData, len(tempData) - 2);
	}
	else if(tempCommand = "*e ")
	{
		tempCommand = remove(" ", tempData);
		if(lower(tempCommand) = lower("UserInterface "))
		{
			tempCommand = remove(" ", tempData);
			if(lower(tempCommand) = lower("Extensions "))
			{
				tempCommand = remove(" ", tempData);
				if(lower(tempCommand) = lower("Event "))
				{
					tempCommand = remove(" ", tempData);
					if(lower(tempCommand) = lower("Pressed "))
					{
						actionId = mid(tempData, find("\x22", tempData) + 1, find(":", tempData, find("\x22", tempData)) - (find("\x22", tempData) + 1));
						if(actionId = paramWidgetId)
						{
							tempQuote = find("\x22", tempData);
							tempStart = find(":", tempData, tempQuote) + 1;
							tempQuote = find("\x22", tempData, tempStart);
							tempCount = tempQuote - tempStart;
							tempCommand = mid(tempData, tempStart, tempCount);
							for(a = 1 to 5)
							{
								if((tempCommand = paramGroupIds[a]) && (issignaldefined(activeButton[a])))
								{
									activeButton[a] = 1;
									break;
								}
							}
						}
					}
					else if(lower(tempCommand) = lower("Released "))
					{
						actionId = mid(tempData, find("\x22", tempData) + 1, find(":", tempData, find("\x22", tempData)) - (find("\x22", tempData) + 1));
						if(actionId = paramWidgetId)
						{
							tempStart = find(":", tempData, find("\x22", tempData)) + 1;
							tempEnd = find("\x22", tempData, tempStart);
							tempCount = tempEnd - tempStart;
							tempCommand = mid(tempData, tempStart, tempCount);
							for(a = 1 to 5)
							{
								if((tempCommand = paramGroupIds[a]) && (issignaldefined(activeButton[a])))
								{
									activeButton[a] = 0;
									break;
								}
							}
						}
					}
				}
			}
		}
	}
}
/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
PUSH buttonActive
{
	integer _buttonActive;
	if(initComplete = 1)
	{
		_buttonActive = getlastmodifiedarrayindex();
		makestring(toProcessorModule, "xCommand UserInterface Extensions Widget SetValue Value: \x22%s\x22 WidgetId: \x22%s\x22\n", paramGroupIds[_buttonActive], paramWidgetId);
	}
}

push unsetButtons
{
	if(initComplete = 1)
	{
		makestring(toProcessorModule, "xCommand UserInterface Extensions Widget UnsetValue WidgetId: \x22%s\x22\n", paramWidgetId);
	}
}

threadsafe CHANGE fromProcessorModule
{
	string tempData[250], tempCommand[250];

	while(1)
	{
		try
		{
			tempData = gather("\x0D\x0A", fromProcessorModule);
			processFromProcessor(tempData);
		}
		catch
		{
			generateusernotice("Cisoc Touch 10 Room Control Group Buttons: Error processing fromProcessorModule: %s", getexceptionmessage());
		}
	}
}
/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
Function Main()
{
	WaitForInitializationComplete();
	error = 0;
	errorMessage = "";
	initComplete = 0;
	initialized = initComplete;
}
